;# This is a rough sketch of the Tindalwic format. Some people will find this format
;# easier to use than the English description in the manifesto. They are the audience
;# for this file. It is incomplete because INDENT/DEDENT are impossible to express with
;# rules, so it is not intended to be used by any of the ABNF tools.

;# The Python parser is a canonical example of a custom lexer that emits INDENT/DEDENT
;# tokens. That kind of language greedily consumes all the whitespace at the start of
;# each line. But Tindalwic is different: only TAB chars count, and the number is
;# strictly monotonic. INDENT adds exactly one TAB, and DEDENT removes exactly one TAB.

;# Because this file is not meant for tools, a non-standard EXCLUDE is imagined...

LF = %x0A
TAB = %x09
dot = EXCLUDE( %x00-FF, LF ) ;# these rules do not validate/decode the 8-bit UTF-8
restricted = EXCLUDE( dot, TAB / "#<>[]{}/=" )

line = *dot LF
utf8 = INDENT *line DEDENT ;# first char of line can be TAB
comment = "#" line [utf8]

;# linear arrays

list = INDENT [comment] *list-item DEDENT
list-item = ( list-text / list-list / list-dict / list-short ) [comment]
list-text = "<>" LF [utf8]
list-list = "[]" LF [list]
list-dict = "{}" LF [dict]
list-short = [restricted *dot] LF

;# associative arrays

key-comment = [LF] "//" line [utf8]

dict = INDENT [comment] *dict-item DEDENT
dict-item = ( dict-text / dict-list / dict-dict / dict-short ) [comment]
dict-text = [key-comment] "<" *dot ">" LF [utf8]
dict-list = [key-comment] "[" *dot "]" LF [list]
dict-dict = [key-comment] "{" *dot "}" LF [dict]
dict-short = [key-comment] restricted *( EXCLUDE( dot, "=" ) ) "=" line

;# outermost context

hashbang = "#!" line [utf8]
root = [hashbang] [comment] *dict-item
